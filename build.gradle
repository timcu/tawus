apply plugin: "idea"

ext.tapestryVersion = "5.3.8"
ext.testngVersion = "5.14.9"
ext.nexusUsername = "tawus"
ext.nexusPassword = "liridwm"

subprojects {
    apply plugin: "java"
    apply plugin: "maven"
    apply plugin: "idea"

    group = "com.googlecode.tawus"
    //version 0.3 for tapestry 5.3.x
    //version 0.4 for tapestry 5.4.x
    version = "0.3"
    sourceCompatibility = '1.5'

    targetCompatibility = '1.5'

    test {
        useTestNG()
        options.suites("src/test/conf/testng.xml")
        maxHeapSize = "400M"
        systemProperties["tapestry.service-reloading-enabled"] = "false"
        jvmArgs("-XX:MaxPermSize=512m")
        logging.captureStandardOutput LogLevel.INFO
        getLogging().setLevel(LogLevel.INFO)
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
        	url "https://repository.apache.org/content/repositories/staging/"
        }
        maven {
        	url "http://repository.jboss.org/nexus/content/groups/public-jboss/"
    	}
    }

    dependencies {
        compile "org.apache.tapestry:tapestry-core:$tapestryVersion"
        testCompile "org.apache.tapestry:tapestry-test:$tapestryVersion"
        compile "org.slf4j:slf4j-api:1.6.1"
        compile "org.slf4j:slf4j-log4j12:1.6.1"
        compile "javax.servlet:servlet-api:2.5"
    }

    // custom tasks for creating source/javadoc jars
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    // add javadoc/source jar tasks as artifacts
    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    uploadArchives {
        repositories.mavenDeployer {
            configuration = configurations.archives

            repository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: nexusUsername, password: nexusPassword)
            }

            pom.project {
                name 'Tawus: A Support library for Tapestry5'
                packaging 'jar'
                description 'Tawus is a support library for Tapestry5 '
                url 'http://github.com/tawus/tawus'
                inceptionYear '2011'

                scm {
                    url 'git@github.com:tawus/tawus.git'
                    connection 'scm:git:git@github.com:tawus/tawus.git'
                    developerConnection "scm:git:git@github.com:tawus/tawus.git"
                }

                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'tawus'
                        name 'Taha Hafeez Siddiqi'
                        email 'tawushafeez@gmail.com'
                        url 'http://tawus.wordpress.com'
                        roles {
                            role 'Developer'
                        }
                        timezone '+5:30'
                    }
                }
            }
        }
    }

}
